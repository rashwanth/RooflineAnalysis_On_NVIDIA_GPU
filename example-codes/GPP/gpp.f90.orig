
program sigma_gpp_gpu 

  use sigma_gpp_data_module

  implicit none

  integer :: igp, iw
  integer(kind=8) :: my_igp, n1_loc, ig
  complex(DPC) :: Omega2, wtilde2, wtilde, delw, wdiff, cden
  complex(DPC) :: ssx_array, sch_array
  complex(DPC) :: ssx, sch, schtt, matngmatmgp
  real(DP) :: delwr, delw2, wdiffr, rden, ssxcutoff
  real :: start, finish

  call setvbuf3f(6,2,0)

  call initialize_data()

  !$ACC DATA COPYIN(n1true_array,occ_array,wx_array_t,aqsmtemp_local,vcoul_loc,wtilde_array,I_eps_array,aqsntemp)

  call cpu_time(start)

  do iw = nstart, nend ! 2

     ssx_array = (0.0d0,0.0d0)
     sch_array = (0.0d0,0.0d0)

     !$ACC PARALLEL PRESENT(I_eps_array, aqsntemp)
     !$ACC LOOP GANG VECTOR reduction(+:ssx_array,sch_array) collapse(2)
     do igp = 1, ngpown ! O(1000)
        do ig = 1, ncouls ! O(10000)
           !$ACC LOOP SEQ
           do n1_loc = 1, ntband_dist ! O(1000)

              wtilde = wtilde_array(ig,igp)
              wtilde2 = wtilde**2
              Omega2 = wtilde2 * I_eps_array(ig,igp)

              wdiff = wx_array_t(iw,n1_loc) - wtilde

              delw = wtilde / wdiff
              delwr = delw * CONJG(delw)
              wdiffr = wdiff * CONJG(wdiff)

              if (wdiffr > limittwo .and. delwr < limitone) then
                 sch = delw * I_eps_array(ig,igp)
                 cden = wx_array_t(iw,n1_loc)**2 - wtilde2
                 ssx = Omega2 / cden
              else if (delwr > TOL_Zero) then
                 sch = 0.0d0
                 cden = (4.0d0 * wtilde2 * (delw + 0.5D0 ))
                 ssx = -Omega2 * delw / cden
              else
                 sch = 0.0d0
                 ssx = 0.0d0
              endif

              matngmatmgp = conjg(aqsmtemp_local(igp,n1_loc)) * aqsntemp(ig,n1_loc)

              ssxcutoff = sexcut * abs(I_eps_array(ig,igp))
              if (abs(ssx) .gt. ssxcutoff .and. wx_array_t(iw,n1_loc) .lt. 0.0d0) ssx=0.0d0

              ssx_array = ssx_array + vcoul_loc(igp) * occ_array(n1_loc) * ssx * matngmatmgp
              sch_array = sch_array + vcoul_loc(igp) * sch * matngmatmgp * 0.5d0

           enddo ! n1_loc
        enddo ! ig
     enddo ! igp
     !$ACC END PARALLEL

     asxtemp(iw) = asxtemp(iw) - ssx_array
     achtemp(iw) = achtemp(iw) + sch_array

  end do ! iw

  call cpu_time(finish)

  print '("Time = ",f7.3," seconds.")', finish - start

  !$ACC END DATA

  if ((abs(asxtempref(nstart) - asxtemp(nstart)) + abs(asxtempref(nend) - asxtemp(nend))) < TOL_Zero) then
     write(6,*) 'asxtemp correct!'
  else
     write(6,*) 'asxtemp incorrect :-('
  endif

  if ((abs(achtempref(nstart) - achtemp(nstart)) + abs(achtempref(nend) - achtemp(nend))) < TOL_Zero) then
     write(6,*) 'achtemp correct!'
  else
     write(6,*) 'achtmp incorrect :-('
  endif

  call finalize_data()

end program sigma_gpp_gpu
